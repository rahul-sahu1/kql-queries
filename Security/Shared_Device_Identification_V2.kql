// Title: Shared Device Identification
// Description: Identifies devices where multiple unique users (excluding a whitelist) have logged on. Useful for finding unauthorized shared workstations or kiosks.
// Category: Security / Audit

let WhitelistedAccounts = dynamic([
    "user1@example.com",
    "user2@example.com",
    "admin@example.com"
    // ... add authorized shared users here
]);
let SharedDevice =
DeviceLogonEvents
| where AccountName contains "@" and AccountName !in (WhitelistedAccounts)
// Filter out specific device naming conventions (e.g., AVDs, Servers)
| where DeviceName !contains "<Excluded-Prefix-1>"
and DeviceName !contains "<Excluded-Prefix-2>"
and DeviceName !contains "<Excluded-Prefix-3>"
| summarize Users = make_set(AccountName), UserCount = dcount(AccountName) by DeviceId
| where UserCount > 1;
DeviceInfo
| where isnotempty(DeviceName)
| where isempty(IsTransient)
| distinct DeviceId, DeviceName
| join kind= inner (SharedDevice) on $left.DeviceId == $right.DeviceId
| project DeviceName, DeviceId, Users, UserCount
